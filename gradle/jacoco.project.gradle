apply plugin: "jacoco"

gradle.projectsEvaluated {
    rootProject.tasks.create("jacocoProjectReport", JacocoReport) {
        group = "Reporting"
        description = "Jacoco test coverage reports for whole project."

        reports {
            csv.required = false
            xml.required = true
            html.required = true
        }

        def moduleReports = rootProject.subprojects.findAll {
            isSourcesModule(it) && it.tasks.findByName("jacocoModuleReport")
        }

        def testReports = rootProject.subprojects.findAll {
            isSourcesModule(it) && it.hasProperty("jacocoTestReport")
        }

        dependsOn(testReports.jacocoTestReport)
        dependsOn(moduleReports.jacocoModuleReport)

        sourceDirectories.setFrom files(moduleReports.jacocoModuleReport.sourceDirectories)
        classDirectories.setFrom files(moduleReports.jacocoModuleReport.classDirectories)
        executionData.setFrom files(moduleReports.jacocoModuleReport.executionData)
    }
}
